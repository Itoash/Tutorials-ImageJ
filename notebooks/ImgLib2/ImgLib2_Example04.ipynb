{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# ImgLib2 \n",
    "\n",
    "Example 04: Specialized iterables"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Example 4 will focus on how to work with specialized **iterables**. They are especially useful when performing operations in the local neighborhood of many pixels - like finding local minima/maxima, texture analysis, convolution with non-separable, non-linear filters and many more. One elegant solution is to write a specialized [**Iterable**](https://docs.oracle.com/javase/8/docs/api/java/lang/Iterable.html) that will iterate all pixels in the local neighborhood. We implemented two examples:\n",
    "\n",
    "* A [**HyperSphere**](http://javadoc.imagej.net/ImgLib2/net/imglib2/algorithm/region/hypersphere/HyperSphere.html) that will iterate an n-dimensional sphere with a given radius at a defined location..\n",
    "* A [**LocalNeighborhood**](http://javadoc.imagej.net/ImgLib2/net/imglib2/algorithm/neighborhood/Neighborhood.html) that will iterate n-dimensionally all pixels adjacent to a certain location, but skip the central pixel (this corresponds to neighbors in 1d, an 8-neighborhood in 2d, a 26-neighborhood in 3d, and so on ...)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "// Behind a firewall? Configure your proxy settings here.\n",
    "//systemProperties = System.getProperties()\n",
    "//systemProperties.setProperty(\"http.proxyHost\",\"myproxy.domain\")\n",
    "//systemProperties.setProperty(\"http.proxyPort\",\"8080\")\n",
    "\n",
    "//////////////////////////////////////////////////////////////\n",
    "// Load ImageJ. This may take some minutes the first time   //\n",
    "// while ImageJ is installed into ~/.groovy/grapes locally. //\n",
    "//////////////////////////////////////////////////////////////\n",
    "@GrabResolver(name='imagej', root='http://maven.imagej.net/content/groups/public/')\n",
    "@Grab('net.imagej:imagej:2.0.0-rc-58')\n",
    "import net.imagej.ImageJ\n",
    "ij = new ImageJ()\n",
    "println(\"ImageJ is ready to go.\")"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Example 4a - Drawing a sphere full of spheres"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "In the first sample we simply draw a sphere full of little spheres. We therefore create a large [**HyperSphere**](http://javadoc.imagej.net/ImgLib2/net/imglib2/algorithm/region/hypersphere/HyperSphere.html) in the center of a [**RandomAccessibleInterval**](http://javadoc.imagej.net/ImgLib2/net/imglib2/RandomAccessibleInterval.html). Note that the **HyperSphere** only needs a [**RandomAccessible**](http://javadoc.imagej.net/ImgLib2/net/imglib2/RandomAccessible.html), we need the additional [**Interval**](http://javadoc.imagej.net/ImgLib2/net/imglib2/util/Intervals.html) simply to compute the center and the radius of the large sphere. When iterating over all pixels of this large sphere, we create a small [**HyperSphere**](http://javadoc.imagej.net/ImgLib2/net/imglib2/algorithm/region/hypersphere/HyperSphere.html) at every n'th pixel and fill them with a random intensity.\n",
    "\n",
    "This example illustrates the use of specialized **Iterables**, and emphasizes the fact that they can be stacked on the underlying [**RandomAccessible**](http://javadoc.imagej.net/ImgLib2/net/imglib2/RandomAccessible.html) using the location of one as the center of a new one. Note that we always create new instances of [**HyperSphere**](http://javadoc.imagej.net/ImgLib2/net/imglib2/algorithm/region/hypersphere/HyperSphere.html) . The code reads very nicely but might not offer the best performance. We therefore added *update* methods to the [**HyperSphere**](http://javadoc.imagej.net/ImgLib2/net/imglib2/algorithm/region/hypersphere/HyperSphere.html)  and its **Cursor** that could be used instead.\n",
    "\n",
    "Another interesting aspect of this example is the use of the [**ImagePlusImgFactory**](http://javadoc.imagej.net/ImgLib2/net/imglib2/img/imageplus/ImagePlusImgFactory.html), which is the compatibility container for ImageJ. If the required dimensionality and [**Type**](http://javadoc.imagej.net/ImgLib2/net/imglib2/type/Type.html) is available in ImageJ, it will internally create an ImagePlus and work on it directly. In this case, one can request the ImagePlus and show it directly. It will, however, fail if [**Type**](http://javadoc.imagej.net/ImgLib2/net/imglib2/type/Type.html) and dimensionality are not supported by ImageJ and throw a [**ImgLibException**](http://javadoc.imagej.net/ImgLib2/net/imglib2/exception/ImgLibException.html)."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "import java.util.Random\n",
    "import net.imglib2.Point\n",
    "import net.imglib2.RandomAccessibleInterval\n",
    "import net.imglib2.algorithm.region.hypersphere.HyperSphere\n",
    "import net.imglib2.algorithm.region.hypersphere.HyperSphereCursor\n",
    "import net.imglib2.exception.ImgLibException\n",
    "import net.imglib2.type.numeric.RealType\n",
    "import net.imglib2.util.Util\n",
    " \n",
    "/**\n",
    " * Example 4a - Drawing a sphere full of spheres\n",
    " *\n",
    " * @author Stephan Preibisch & Stephan Saalfeld\n",
    " *\n",
    " */\n",
    "construct01 = new Object() {\n",
    "  \n",
    "  /**\n",
    "    * Draws a sphere that contains lots of small spheres into the center of the\n",
    "    * interval.\n",
    "    *\n",
    "    * @param <T> value type\n",
    "    * @param randomAccessible - the image data to write to\n",
    "    * @param minValue - the minimal intensity of one of the small spheres\n",
    "    * @param maxValue - the maximal intensity of one of the small spheres\n",
    "   */\n",
    "  public <T extends RealType<T>> void drawSpheres(final RandomAccessibleInterval<T> randomAccessible, final double minValue, final double maxValue) {\n",
    "    // the number of dimensions\n",
    "    int numDimensions = randomAccessible.numDimensions()\n",
    "    \n",
    "    // define the center and radius\n",
    "    Point center = new Point(randomAccessible.numDimensions())\n",
    "    int minSize = randomAccessible.dimension(0)\n",
    "    \n",
    "    for (int d = 0; d < numDimensions; ++d) {\n",
    "      int size = randomAccessible.dimension(d)\n",
    "      int half = size / 2\n",
    "      center.setPosition(half, d)\n",
    "      minSize = Math.min(minSize, size)\n",
    "    }\n",
    "    \n",
    "    // define the maximal radius of the small spheres\n",
    "    final int maxRadius = 5\n",
    "    \n",
    "    // compute the radius of the large sphere so that we do not draw\n",
    "    // outside of the defined interval\n",
    "    final long radiusLargeSphere = minSize / 2 - maxRadius - 1\n",
    "    \n",
    "    // instantiate a random number generator\n",
    "    final Random rnd = new Random(System.currentTimeMillis())\n",
    "    \n",
    "    // define a hypersphere (n-dimensional sphere)\n",
    "    HyperSphere<T> hyperSphere = new HyperSphere<T>(randomAccessible, center, radiusLargeSphere)\n",
    "    \n",
    "    // create a cursor on the hypersphere\n",
    "    HyperSphereCursor<T> cursor = hyperSphere.cursor()\n",
    "\n",
    "    while (cursor.hasNext()) {\n",
    "      cursor.fwd()\n",
    "      \n",
    "      // the random radius of the current small hypersphere\n",
    "      final int radius = rnd.nextInt(maxRadius) + 1\n",
    "      \n",
    "      // instantiate a small hypersphere at the location of the current pixel in the large hypersphere\n",
    "      HyperSphere<T> smallSphere = new HyperSphere<T>(randomAccessible, cursor, radius)\n",
    "      \n",
    "      // define the random intensity for this small sphere\n",
    "      final double randomValue = rnd.nextDouble()\n",
    "      \n",
    "      // take only every 4^dimension'th pixel by chance so that it is not too crowded\n",
    "      if (Math.round(randomValue * 100) % Util.pow(4, numDimensions) == 0) {\n",
    "        // scale to right range\n",
    "        randomValue = rnd.nextDouble() * (maxValue - minValue) + minValue\n",
    "        \n",
    "        // set the value to all pixels in the small sphere if the intensity is brighter than the existing one\n",
    "        for (final T value : smallSphere) {\n",
    "          value.setReal(Math.max(randomValue, value.getRealDouble()))\n",
    "        }\n",
    "      }\n",
    "    }\n",
    "  }\n",
    "}"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "**Note:** The current display method can only display the first frame of multi-framed images, so we only see a \"black\" image."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "import net.imglib2.type.numeric.integer.UnsignedByteType\n",
    "import net.imglib2.img.Img;\n",
    "import net.imglib2.img.ImgFactory;\n",
    "import net.imglib2.img.array.ArrayImgFactory;\n",
    "\n",
    "long[] dims = [256, 256, 256]\n",
    "ImgFactory<UnsignedByteType> imgFactory = new ArrayImgFactory<UnsignedByteType>();\n",
    "Img<UnsignedByteType> img = imgFactory.create(dims, new UnsignedByteType());\n",
    "\n",
    "// draw a small sphere for every pixel of a larger sphere\n",
    "int minv = 0\n",
    "int maxv = 255\n",
    "construct01.drawSpheres(img, minv, maxv)\n",
    "\n",
    "// show the hyperspheres\n",
    "ij.notebook().display(img)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Example 4b - Finding and displaying local minima"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "In this example we want to find all local minima in an image and display them as small spheres. To not capture too much of the noise in the image data, we first perform an in-place Gaussian smoothing with a sigma of 1, i.e. the data will be overwritten with the result. A complete documentation of the gauss package for ImgLib2 can be found  [here](http://imagej.net/Gauss_Package_ImgLib2).\n",
    "\n",
    "We display the results using a binary image. *Note that the [**BitType**](http://javadoc.imagej.net/ImgLib2/net/imglib2/type/logic/BitType.html) only requires one bit per pixel and therefore is very memory efficient.*\n",
    "\n",
    "The generic method for minima detection has some more interesting properties. The type of the source image data actually does not require to be of [**Type**](http://javadoc.imagej.net/ImgLib2/net/imglib2/type/Type.html), it simply needs something that is comparable. The [**LocalNeighborhood**](http://javadoc.imagej.net/ImgLib2/net/imglib2/algorithm/neighborhood/Neighborhood.html) will iterate n-dimensionally all pixels adjacent to a certain location, but skip the central pixel (this corresponds to neighbors in 1d, an 8-neighborhood in 2d, a 26-neighborhood in 3d, and so on ...). This allows to efficiently detect if a pixel is a local minima or maxima. Note that the [**Cursor**](http://javadoc.imagej.net/ImgLib2/net/imglib2/Cursor.html) that performs the iteration can have special implementations for specific dimensionalities to speed up the iteration. See below the example for a specialized three-dimensional iteration:\n",
    "\n",
    "Please note as well that if one would increase the radius of the [**RectangleShape**](http://javadoc.imagej.net/ImgLib2/net/imglib2/algorithm/neighborhood/RectangleShape.html) to more than 1 (without at the same time changing the [**View**](http://javadoc.imagej.net/ImgLib2/net/imglib2/View.html) on source that creates an inset border of exactly this one pixel), this example would fail as we would try to write image data outside of the defined boundary. [**OutOfBoundsStrategies**](http://javadoc.imagej.net/ImgLib2/net/imglib2/view/Views.html) which define how to handle such cases is discussed in example 5."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "import net.imglib2.Cursor\n",
    "import net.imglib2.Interval\n",
    "import net.imglib2.RandomAccessibleInterval\n",
    "import net.imglib2.algorithm.gauss.Gauss\n",
    "import net.imglib2.algorithm.neighborhood.Neighborhood\n",
    "import net.imglib2.algorithm.neighborhood.RectangleShape\n",
    "import net.imglib2.algorithm.region.hypersphere.HyperSphere\n",
    "import net.imglib2.img.Img\n",
    "import net.imglib2.img.ImgFactory\n",
    "import net.imglib2.img.array.ArrayImgFactory\n",
    "import net.imglib2.type.NativeType\n",
    "import net.imglib2.type.logic.BitType\n",
    "import net.imglib2.type.numeric.RealType\n",
    "import net.imglib2.util.Intervals\n",
    "import net.imglib2.view.Views\n",
    " \n",
    "construct02 = new Object() {\n",
    "  /**\n",
    "    * Checks all pixels in the image if they are a local minima and draws a circle into the output if they are\n",
    "    *\n",
    "    * @param <T> value type\n",
    "    * @param <U>\n",
    "    * @param source - the image data to work on\n",
    "    * @param imageFactory - the factory for the output img\n",
    "    * @param outputType - the output type\n",
    "    * @return - an Img with circles on locations of a local minimum\n",
    "  */\n",
    "  public static <T extends Comparable<T>, U extends RealType<U>> Img<U> findAndDisplayLocalMinima(\n",
    "      RandomAccessibleInterval<T> source, ImgFactory<U> imageFactory, U outputType) {\n",
    "    // Create a new image for the output\n",
    "    Img<U> output = imageFactory.create(source, outputType)\n",
    "    \n",
    "    // define an interval that is one pixel smaller on each side in each dimension,\n",
    "    // so that the search in the 8-neighborhood (3x3x3...x3) never goes outside\n",
    "    // of the defined interval\n",
    "    Interval interval = Intervals.expand(source, -1)\n",
    "    \n",
    "    // create a view on the source with this interval\n",
    "    source = Views.interval(source, interval)\n",
    "    \n",
    "    // create a Cursor that iterates over the source and checks in an 8-neighborhood if it is a minima\n",
    "    final Cursor<T> center = Views.iterable(source).cursor()\n",
    "    \n",
    "    // instantiate a RectangleShape to access rectangular local neighborhoods\n",
    "    // of radius 1 (that is 3x3x...x3 neighborhoods), skipping the center pixel\n",
    "    // (this corresponds to an 8-neighborhood in 2d or 26-neighborhood in 3d, ...)\n",
    "    final RectangleShape shape = new RectangleShape(1, true)\n",
    "    \n",
    "    // iterate over the set of neighborhoods in the image\n",
    "    for (final Neighborhood<T> localNeighborhood : shape.neighborhoods(source)) {\n",
    "      // what is the value that we investigate?\n",
    "      // (the center cursor runs over the image in the same iteration order as neighborhood)\n",
    "      final T centerValue = center.next()\n",
    "      \n",
    "      // keep this boolean true as long as no other value in the local neighborhood\n",
    "      // is larger or equal\n",
    "      boolean isMinimum = true\n",
    "      \n",
    "      // check if all pixels in the local neighborhood that are smaller\n",
    "      for (final T value : localNeighborhood) {\n",
    "        // test if the center is smaller than the current pixel value\n",
    "        if (centerValue.compareTo(value) >= 0) {\n",
    "          isMinimum = false\n",
    "          break\n",
    "        }\n",
    "      }\n",
    "      \n",
    "      if (isMinimum) {\n",
    "        // draw a sphere of radius one in the new image\n",
    "        HyperSphere<U> hyperSphere = new HyperSphere<U>(output, center, 1)\n",
    "        \n",
    "        // set every value inside the sphere to 1\n",
    "        for (U value : hyperSphere) {\n",
    "          value.setOne()\n",
    "        }\n",
    "      }\n",
    "    }\n",
    "    \n",
    "    return output\n",
    "  }\n",
    "}"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "import io.scif.img.ImgOpener\n",
    "import net.imglib2.img.Img\n",
    "import net.imglib2.algorithm.gauss.Gauss\n",
    "import net.imglib2.type.logic.BitType\n",
    "import net.imglib2.img.array.ArrayImgFactory\n",
    "import net.imglib2.type.numeric.real.FloatType\n",
    "\n",
    "img = new ImgOpener().openImgs(\"http://samples.fiji.sc/tutorials/DrosophilaWing.tif\").get(0)\n",
    "\n",
    "// first we do a small in-place gaussian smoothing with a sigma of 1.0\n",
    "double[] sigmas = [1.0, 1.0]\n",
    "Gauss.inDoubleInPlace(sigmas, img)\n",
    "\n",
    "// find local minima and paint them into another image as spheres\n",
    "featureMap = construct02.findAndDisplayLocalMinima(img, new ArrayImgFactory<BitType>(), new BitType())\n",
    "\n",
    "// show the hyperspheres\n",
    "ij.notebook().display(featureMap, 0, 1)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Groovy",
   "language": "groovy",
   "name": "groovy"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
