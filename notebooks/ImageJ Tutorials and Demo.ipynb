{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# ImageJ Tutorials and Demo\n",
    "\n",
    "Welcome to the ImageJ tutorial series. These notebooks offer a hands-on series of lessons for learning ImageJ.\n",
    "\n",
    "* For a quick demo of what ImageJ can do, ___just scroll down___!\n",
    "\n",
    "* To dive in to the tutorials, click the links below. If ImageJ is new to you, please try the \"Using ImageJ\" notebooks first.\n",
    "\n",
    "* For a thorough academic overview of the ImageJ software stack, including its features, history, motivation and goals, see:\n",
    "  > Rueden, C. T., et al. \"[ImageJ2: ImageJ for the next generation of scientific image data](https://arxiv.org/abs/1701.05940).\" arXiv preprint (2017).\n",
    "  \n",
    "* Learn more about ImageJ at [imagej.net](https://imagej.net/). Learn about [Beaker Notebook](http://beakernotebook.com/) from its tutorials: Help â†’ Tutorial.\n",
    "\n",
    "Feedback is very welcome! Please start a discussion with us on the [ImageJ Forum](http://forum.imagej.net/)!"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%html\n",
    "<script>\n",
    "  function absPath(path) {\n",
    "    var absPath = document.getElementsByClassName('path-name').item(0).innerHTML;\n",
    "    return absPath.replace(/^\\*/, '').replace(/\\\\/g, '/').replace(/\\/[^\\/]*$/, path);\n",
    "  }\n",
    "  function link(id, path) {\n",
    "    document.getElementById('link-' + id).href = '/beaker/#/open?uri=file:' + escape(absPath(path));\n",
    "  }\n",
    "  link('1.01', '/[1] Using ImageJ/[1.01] Fundamentals of ImageJ.bkr');\n",
    "  link('1.02', '/[1] Using ImageJ/[1.02] Introduction to ImageJ Ops.bkr');\n",
    "  link('1.03', '/[1] Using ImageJ/[1.03] N-dimensional image processing.bkr');\n",
    "  link('1.04', '/[1] Using ImageJ/[1.04] Working with tables.bkr');\n",
    "  link('1.05', '/[1] Using ImageJ/[1.05] Mixed World - Using ImageJ 1.x.bkr');\n",
    "  link('2.01', '/[2] Extending ImageJ/[2.01] Scripting.bkr');\n",
    "  link('2.02', '/[2] Extending ImageJ/[2.02] Writing I-O plugins.bkr');\n",
    "  link('2.03', '/[2] Extending ImageJ/[2.03] Writing Command plugins.bkr');\n",
    "  link('2.04', '/[2] Extending ImageJ/[2.04] Writing Op plugins.bkr');\n",
    "  link('2.05', '/[2] Extending ImageJ/[2.05] Writing Tool plugins.bkr');\n",
    "  link('3.01', '/[3] Advanced usage/[3.01] The Architecture of SciJava.bkr');\n",
    "  link('3.02', '/[3] Advanced usage/[3.02] Under the hood - SciJava.bkr');\n",
    "  link('3.03', '/[3] Advanced usage/[3.03] Under the hood - ImgLib2.bkr');\n",
    "  link('3.04', '/[3] Advanced usage/[3.04] Under the hood - ImageJ.bkr');\n",
    "  link('3.05', '/[3] Advanced usage/[3.05] Under the hood - SCIFIO.bkr');\n",
    "  link('4.01', '/[4] Advanced extensions/[4.01] Writing a reusable op.bkr');\n",
    "  link('4.02', '/[4] Advanced extensions/[4.02] Creating a custom service.bkr');\n",
    "  link('4.03', '/[4] Advanced extensions/[4.03] Customizing module execution.bkr');\n",
    "  link('4.04', '/[4] Advanced extensions/[4.04] Creating a SCIFIO plugin.bkr');\n",
    "</script>\n",
    "<div class=\"row\">\n",
    "  <div class=\"col-md-12\" style=\"border-top: solid 1px #888; margin-top: 20px;\">\n",
    "  </div>\n",
    "</div>\n",
    "<div class=\"row\">\n",
    "  <div class=\"col-md-6\">\n",
    "    <h2>1. Using ImageJ</h2>\n",
    "    <ol>\n",
    "      <li><a href=\"#\" id=\"link-1.01\">Fundamentals of ImageJ</a></li>\n",
    "      <li><a href=\"#\" id=\"link-1.02\">Introduction to ImageJ Ops</a></li>\n",
    "      <li><a href=\"#\" id=\"link-1.03\">N-dimensional image processing</a> (incomplete)</li>\n",
    "      <li><a href=\"#\" id=\"link-1.04\">Working with tables</a> (coming later)</li>\n",
    "      <li><a href=\"#\" id=\"link-1.05\">Mixed World: Using ImageJ 1.x</a> (incomplete)</li>\n",
    "    </ol>\n",
    "  </div>\n",
    "  <div class=\"col-md-5\">\n",
    "    <h2>2. Extending ImageJ</h2>\n",
    "    <ol>\n",
    "      <li><a href=\"#\" id=\"link-2.01\">Scripting: the easy way to extend ImageJ</a> (incomplete)</li>\n",
    "      <li><a href=\"#\" id=\"link-2.02\">Extending ImageJ: Data I/O</a> (incomplete)</li>\n",
    "      <li><a href=\"#\" id=\"link-2.03\">Extending ImageJ: Commands</a> (incomplete)</li>\n",
    "      <li><a href=\"#\" id=\"link-2.04\">Extending ImageJ: Ops</a> (incomplete)</li>\n",
    "      <li><a href=\"#\" id=\"link-2.05\">Extending ImageJ: Tools</a> (incomplete)</li>\n",
    "    </ol>\n",
    "  </div>\n",
    "</div>\n",
    "<div class=\"row\">\n",
    "  <div class=\"col-md-6\">\n",
    "    <h2>3. Advanced usage</h2>\n",
    "    <ol>\n",
    "      <li><a href=\"#\" id=\"link-3.01\">The Architecture of SciJava</a> (incomplete)</li>\n",
    "      <li><a href=\"#\" id=\"link-3.02\">Under the hood: SciJava</a> (incomplete)</li>\n",
    "      <li><a href=\"#\" id=\"link-3.03\">Under the hood: ImgLib2</a> (coming later)</li>\n",
    "      <li><a href=\"#\" id=\"link-3.04\">Under the hood: ImageJ</a> (coming later)</li>\n",
    "      <li><a href=\"#\" id=\"link-3.05\">Under the hood: SCIFIO</a> (coming later)</li>\n",
    "    </ol>\n",
    "  </div>\n",
    "  <div class=\"col-md-5\">\n",
    "    <h2>4. Advanced extensions</h2>\n",
    "    <ol>\n",
    "      <li><a href=\"#\" id=\"link-4.01\">Writing a reusable op</a> (coming later)</li>\n",
    "      <li><a href=\"#\" id=\"link-4.02\">Creating a custom service</a> (coming later)</li>\n",
    "      <li><a href=\"#\" id=\"link-4.03\">Customizing module execution</a> (coming later)</li>\n",
    "      <li><a href=\"#\" id=\"link-4.04\">Creating a SCIFIO plugin</a> (coming later)</li>\n",
    "    </ol>\n",
    "  </div>\n",
    "</div>\n",
    "<div class=\"row\">\n",
    "  <div class=\"col-md-12\" style=\"border-top: solid 1px #888; margin-top: 20px;\">\n",
    "  </div>\n",
    "</div>"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# ImageJ Demo"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "// Behind a firewall? Configure your proxy settings here.\n",
    "//System.setProperty(\"http.proxyHost\",\"myproxy.domain\")\n",
    "//System.setProperty(\"http.proxyPort\",\"8080\")\n",
    "\n",
    "//////////////////////////////////////////////////////////////\n",
    "// Load ImageJ. This may take some minutes the first time   //\n",
    "// while ImageJ is installed into ~/.groovy/grapes locally. //\n",
    "//////////////////////////////////////////////////////////////\n",
    "@GrabResolver(name='imagej', root='http://maven.imagej.net/content/groups/public/')\n",
    "@Grab('net.imagej:imagej:2.0.0-rc-58')\n",
    "import net.imagej.ImageJ\n",
    "ij = new ImageJ()\n",
    "\n",
    "// Define some handy shorthands!\n",
    "show = { image ->\n",
    "  ij.notebook().display(image)\n",
    "}\n",
    "import net.imglib2.RandomAccessibleInterval\n",
    "tile = { images ->\n",
    "  int[] gridLayout = images[0] in List ?\n",
    "    [images[0].size, images.size] : // 2D images list\n",
    "    [images.size] // 1D images list\n",
    "  RandomAccessibleInterval[] rais = images.flatten()\n",
    "  mosaic = ij.notebook().mosaic(gridLayout, rais)\n",
    "  ij.notebook().display(mosaic)\n",
    "}\n",
    "\n",
    "println(\"ImageJ is ready to go.\")"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Load some images"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "sourcePath = \"http://imagej.net/images\"\n",
    "//sourcePath = System.getProperty(\"user.home\") + \"/data\"\n",
    "cells = ij.io().open(sourcePath + \"/FluorescentCells.jpg\")\n",
    "lena = ij.io().open(sourcePath + \"/lena.jpg\")\n",
    "tile([cells, lena])"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Compute and display per-channel histograms"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "import net.imglib2.FinalInterval\n",
    "\n",
    "// Set this to the image you want to analyze.\n",
    "image = lena \n",
    "\n",
    "xLen = image.dimension(0)\n",
    "yLen = image.dimension(1)\n",
    "cLen = image.dimension(2)\n",
    "values = []\n",
    "for (c in 0..cLen - 1) {\n",
    "  // Slice the image at this channel.\n",
    "  slice = ij.op().transform().crop(image, FinalInterval.createMinSize(0, 0, c, xLen, yLen, 1))\n",
    "  // Get the histogram.\n",
    "  histogram = ij.op().image().histogram(slice)\n",
    "\n",
    "// Extract the counts to a Groovy-friendly data structure.\n",
    "  counts = []\n",
    "  for (value in histogram)\n",
    "    counts.add(value.getRealDouble())\n",
    "  values.add(counts)\n",
    "}\n",
    "  \n",
    "// Create the plot.\n",
    "def cplot = new CategoryPlot(initWidth: 600, initHeight: 300)\n",
    "def bars = new CategoryLines(\n",
    "  value: values,\n",
    "  seriesNames: [\"Red\", \"Green\", \"Blue\"],\n",
    "  color: [Color.red, Color.green, Color.blue]\n",
    ")\n",
    "cplot << bars"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "<font size=\"+1\">__Quiz!__ What is wrong with those histograms?</font>"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "import net.imglib2.FinalInterval\n",
    "\n",
    "// N-dimensional crop.\n",
    "face = ij.op().transform().crop(lena, FinalInterval.createMinSize(200, 200, 0, 170, 195, 1), true)\n",
    "\n",
    "// Type conversion.\n",
    "face32 = ij.op().convert().float32(face)\n",
    "\n",
    "// Median filter.\n",
    "median = ij.op().run(\"create.img\", face32)\n",
    "neighborhood = new HyperSphereShape(4)\n",
    "ij.op().run(\"filter.median\", median, face32, neighborhood)\n",
    "\n",
    "// Difference of Gaussians.\n",
    "dogFormula = \"gauss(image, sigma1) - gauss(image, sigma2)\"\n",
    "dog = ij.op().eval(dogFormula, [\n",
    "  \"image\": face32,\n",
    "  \"sigma1\": [20, 20],\n",
    "  \"sigma2\": [4, 4]\n",
    "])\n",
    "\n",
    "// Grayscale morphology operators.\n",
    "import net.imglib2.algorithm.neighborhood.HyperSphereShape\n",
    "topHat = ij.op().morphology().topHat(face, [neighborhood])\n",
    "topHat32 = ij.op().convert().float32(topHat)\n",
    "blackTopHat = ij.op().morphology().blackTopHat(face, [neighborhood])\n",
    "blackTopHat32 = ij.op().convert().float32(blackTopHat)\n",
    "\n",
    "tile([face32, median, dog, topHat32, blackTopHat32])"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Fourier transform with lowpass filter"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "import net.imglib2.util.Util\n",
    "import net.imglib2.FinalDimensions\n",
    "\n",
    "image = cells\n",
    "radius = 10\n",
    "\n",
    "def lowpass(fft, radius) {\n",
    "  // Declare an array to hold the current position of the cursor.\n",
    "  pos = new long[fft.numDimensions()]\n",
    "\n",
    "  // Define origin as 0,0.\n",
    "  long[] origin = [0, 0]\n",
    "\n",
    "  // Define a 2nd 'origin' at bottom left of image.\n",
    "  // This is a bit of a hack. We want to draw a circle around the origin,\n",
    "  // since the origin is at 0,0 - the circle will 'reflect' to the bottom.\n",
    "  long[] origin2 = [0, fft.dimension(1)]\n",
    "\n",
    "  // Loop through all pixels.\n",
    "  cursor = fft.localizingCursor()\n",
    "  while (cursor.hasNext()) {\n",
    "    cursor.fwd()\n",
    "    cursor.localize(pos)\n",
    "\n",
    "    // Calculate distance from 0,0 and bottom left corner\n",
    "    // (so we can form the reflected semi-circle).\n",
    "    dist = Util.distance(origin, pos)\n",
    "    dist2 = Util.distance(origin2, pos)\n",
    "\n",
    "    // If distance is above radius (cutoff frequency) set value of FFT to zero.\n",
    "    if (dist > radius && dist2 > radius)\n",
    "    cursor.get().setZero()\n",
    "  }\n",
    "}\n",
    "\n",
    "// Perform fft of the input.\n",
    "fft = ij.op().filter().fft(image)\n",
    "\n",
    "// Filter it.\n",
    "lowpass(fft, 10)\n",
    "\n",
    "// Reverse the FFT.\n",
    "import net.imglib2.type.numeric.real.FloatType\n",
    "inverse = ij.op().run(\"create.img\", image, new FloatType())\n",
    "ij.op().filter().ifft(inverse, fft)\n",
    "\n",
    "// Display the result.\n",
    "image32 = ij.op().convert().float32(image)\n",
    "tile([image32, inverse])"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Groovy",
   "language": "groovy",
   "name": "groovy"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
